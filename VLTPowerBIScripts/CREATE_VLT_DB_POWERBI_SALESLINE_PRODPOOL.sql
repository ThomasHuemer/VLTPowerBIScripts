USE [VLT_AX500105_P]
GO

/****** Object:  View [dbo].[VLT_DB_POWERBI_SALESLINE]    Script Date: 23.07.2018 10:30:07 ******/
IF EXISTS (
		SELECT
			1
		FROM sys.VIEWS
		WHERE NAME = 'VLT_DB_POWERBI_SALESLINE_PRODPOOL'
			AND type = 'V'
	)
	DROP VIEW [dbo].[VLT_DB_POWERBI_SALESLINE_PRODPOOL]
GO

/****** Object:  View [dbo].[VLT_DB_POWERBI_SALESLINE_PRODPOOL]    Script Date: 23.07.2018 10:30:07 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE VIEW [dbo].[VLT_DB_POWERBI_SALESLINE_PRODPOOL]
AS
	SELECT
		SALESLINE.SALESID,
		SALESLINE.SALESQTY,
		SALESLINE.LINEAMOUNT,
		SALESLINE.ITEMID,
		SALESLINE.NAME,
		InventTable.ITEMGROUPID,
		InventTable.ITEMTYPE,
		InventItemGroup.NAME																																	ItemGroupName,
		SALESLINE.SALESBOQID,
		SALESLINE.LINENUM,
		SALESLINE.SALESID + SALESLINE.DATAAREAID																												AS FKSalesTable,
		SALESLINE.ITEMID + SALESLINE.DATAAREAID																													AS FKInventTable,
		InventTable.PRODPOOLID																																	AS PRODPOOL,
		SALESLINE.RECID																																			AS FKRecId,
		DATEPART(ISO_WEEK, SALESLINE.SHIPPINGDATECONFIRMED)																										AS DeliveryWeek,
		DATEPART(YEAR, SALESLINE.SHIPPINGDATECONFIRMED)																											AS DeliveryYear,
		dbo.VLT_DB_GETDATETIMEWITHTIMEZONE(SALESLINE.SHIPPINGDATECONFIRMED)																						AS 'Lieferdatum',
		CONVERT(VARCHAR(10), DATEPART(YEAR, SALESLINE.SHIPPINGDATECONFIRMED) * 100) + CONVERT(VARCHAR(10), DATEPART(ISO_WEEK, SALESLINE.SHIPPINGDATECONFIRMED)) AS FKYearWeek,
		SALESTABLE.SALESTYPE,
		CASE
			WHEN SALESTABLE.SALESTYPE = 0 THEN 'Journal'
			WHEN SALESTABLE.SALESTYPE = 1 THEN 'Angebot'
			WHEN SALESTABLE.SALESTYPE = 2 THEN 'Dauerauftrag'
			WHEN SALESTABLE.SALESTYPE = 3 THEN 'Auftrag'
			WHEN SALESTABLE.SALESTYPE = 4 THEN 'Zurückgegebener'
			WHEN SALESTABLE.SALESTYPE = 5 THEN 'Abruf'
			WHEN SALESTABLE.SALESTYPE = 6 THEN 'Artikelbedarf'
		END																																						AS SalesTypeDesc,
		SALESTABLE.SALESSTATUS,
		CASE
			WHEN SALESTABLE.SALESSTATUS = 0 THEN 'Kein'
			WHEN SALESTABLE.SALESSTATUS = 1 THEN 'Offener Auftrag'
			WHEN SALESTABLE.SALESSTATUS = 2 THEN 'Geliefert'
			WHEN SALESTABLE.SALESSTATUS = 3 THEN 'Fakturiert'
			WHEN SALESTABLE.SALESSTATUS = 4 THEN 'Storniert'
		END																																						AS SalesStatusDesc
	FROM dbo.SALESLINE AS SALESLINE
	JOIN SALESTABLE SALESTABLE ON (SALESTABLE.DATAAREAID = SALESLINE.DATAAREAID AND SALESTABLE.SALESID = SALESLINE.SALESID)
	LEFT JOIN dbo.InventTable AS InventTable ON (SALESLINE.DATAAREAID = InventTable.DATAAREAID AND SALESLINE.ITEMID = InventTable.ITEMID)
	LEFT JOIN dbo.InventItemGroup AS InventItemGroup ON (InventTable.DATAAREAID = InventItemGroup.DATAAREAID AND InventTable.ITEMGROUPID = InventItemGroup.ITEMGROUPID)
	WHERE SALESLINE.DATAAREAID = '100'
		AND SALESLINE.CREATEDDATETIME > DATETIMEFROMPARTS(YEAR(GETDATE()) - 3, 1, 1, 0, 0, 0, 0)
GO


